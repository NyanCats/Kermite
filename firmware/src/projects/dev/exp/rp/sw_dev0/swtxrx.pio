.program swtx
    nop [7]
    pull                ;TX FIFOからosrに1word(32bit)を取り込む
    set x, 8            ;ループカウンタ, 8ビット+終端フラグ1ビットで9ビット分出力する
    set pindirs, 1 [7]  ;送信開始 lower
bitloop:
    out y, 1            ;osrの下位から1ビットシフトしてyに格納
    jmp !y do_zero  
    set pindirs, 0 [5]  ;論理1出力 raise
    set pindirs, 1 [0]  ;論理1出力 lower
    jmp end_cycle
do_zero:
    set pindirs, 0 [2]  ;論理0出力 raise
    set pindirs, 1 [1]  ;論理0出力 lower
end_cycle:
    jmp x-- bitloop
    set pindirs, 0      ;送信終了 raise
    nop [5]
    push                ;RX FIFOに空データを吐き出すことで送信完了を呼び出し元に通知


.program swrx
.side_set 1 opt
    wait 0 pin 0
    nop side 0
    set x, 8         ;データ8ビット+終端フラグ1ビットの9ビット分パルスを読み取る
bitloop:
    wait 1 pin 0
    nop side 1 [0]
    in pins 1        ;ピンから読み取った値をisrにシフトして入れる
    nop side 0
    jmp x--, bitloop
    nop [4]
    nop side 1
    push
    